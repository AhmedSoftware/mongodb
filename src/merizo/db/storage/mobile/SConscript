Import("env")
Import("mobile_se")
env = env.Clone()


env.InjectThirdParty(libraries=['sqlite'])

env.Library(
    target='storage_mobile_core',
    source=[
        'mobile_index.cpp',
        'mobile_kv_engine.cpp',
        'mobile_record_store.cpp',
        'mobile_recovery_unit.cpp',
        'mobile_session.cpp',
        'mobile_session_pool.cpp',
        'mobile_sqlite_statement.cpp',
        'mobile_util.cpp',
        'mobile_global_options.cpp',
        'mobile_options_init.cpp',
        env.Idlc('mobile_global_options.idl')[0],
        ],
    LIBDEPS=[
        '$BUILD_DIR/merizo/base',
        '$BUILD_DIR/merizo/db/catalog/collection_options',
        '$BUILD_DIR/merizo/db/concurrency/lock_manager',
        '$BUILD_DIR/merizo/db/concurrency/write_conflict_exception',
        '$BUILD_DIR/merizo/db/index/index_descriptor',
        '$BUILD_DIR/merizo/db/namespace_string',
        '$BUILD_DIR/merizo/db/repl/repl_coordinator_interface',
        '$BUILD_DIR/merizo/db/storage/index_entry_comparison',
        '$BUILD_DIR/merizo/db/storage/journal_listener',
        '$BUILD_DIR/merizo/db/storage/key_string',
        '$BUILD_DIR/merizo/db/storage/kv/kv_prefix',
        '$BUILD_DIR/merizo/db/storage/oplog_hack',
        '$BUILD_DIR/third_party/shim_sqlite',
        ],
    LIBDEPS_PRIVATE= [
        '$BUILD_DIR/merizo/util/options_parser/options_parser',
        ],
    )

serveronlyProgDepsDependents = []
if mobile_se:
    serveronlyProgDepsDependents = [
        '$BUILD_DIR/merizo/merizod',
        '$BUILD_DIR/merizo/dbtests/dbtest',
        ]

env.Library(
    target='storage_mobile',
    source=[
        'mobile_init.cpp',
        ],
    PROGDEPS_DEPENDENTS=serveronlyProgDepsDependents,
    LIBDEPS=[
        'storage_mobile_core',
        '$BUILD_DIR/merizo/db/storage/kv/kv_engine'
        ],
    LIBDEPS_PRIVATE=[
        '$BUILD_DIR/merizo/db/storage/storage_engine_common',
    ],
)
'''
env.CppUnitTest(
    target='storage_mobile_index_test',
    source=[
        'mobile_index_test.cpp'
    ],
    LIBDEPS=[
        'storage_mobile_core',
        '$BUILD_DIR/merizo/db/storage/sorted_data_interface_test_harness'
        ]
    )
'''

env.CppUnitTest(
    target='storage_mobile_record_store_test',
    source=[
        'mobile_record_store_test.cpp'
    ],
    LIBDEPS=[
        'storage_mobile_core',
        '$BUILD_DIR/merizo/db/storage/record_store_test_harness',
        '$BUILD_DIR/merizo/db/concurrency/write_conflict_exception',
    ]
)

env.CppUnitTest(
    target='storage_mobile_kv_engine_test',
    source=[
        'mobile_kv_engine_test.cpp',
    ],
    LIBDEPS=[
        '$BUILD_DIR/merizo/db/storage/kv/kv_engine_test_harness',
        '$BUILD_DIR/merizo/db/storage/storage_options',
        'storage_mobile_core',
    ],
    LIBDEPS_PRIVATE=[
        '$BUILD_DIR/merizo/db/auth/authmocks',
    ],
)
