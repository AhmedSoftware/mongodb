# -*- mode: python -*-
Import("env")
Import("free_monitoring")

env = env.Clone()

fmEnv = env.Clone()
fmEnv.InjectThirdParty(libraries=['snappy'])

fmEnv.Library(
    target='free_mon',
    source=[
        'free_mon_processor.cpp',
        'free_mon_queue.cpp',
        'free_mon_op_observer.cpp',
        'free_mon_storage.cpp',
        'free_mon_controller.cpp',
        env.Idlc('free_mon_protocol.idl')[0],
        env.Idlc('free_mon_commands.idl')[0],
        env.Idlc('free_mon_storage.idl')[0],
    ],
    LIBDEPS=[
        '$BUILD_DIR/merizo/base',
        '$BUILD_DIR/merizo/db/concurrency/lock_manager',
        '$BUILD_DIR/merizo/db/dbhelpers',
        '$BUILD_DIR/merizo/db/ftdc/ftdc',
        '$BUILD_DIR/merizo/idl/idl_parser',
        '$BUILD_DIR/third_party/shim_snappy',
    ],
)

if free_monitoring == "on":
    fmEnv.Library(
        target='free_mon_merizod',
        source=[
            'free_mon_commands.cpp',
            'free_mon_merizod.cpp',
            env.Idlc('free_mon_merizod.idl')[0],
            'free_mon_options.cpp',
            env.Idlc('free_mon_options.idl')[0],
            'free_mon_status.cpp',
        ],
        LIBDEPS=[
            'free_mon',
            '$BUILD_DIR/merizo/db/commands/server_status',
            '$BUILD_DIR/merizo/db/ftdc/ftdc_server',
            '$BUILD_DIR/merizo/util/options_parser/options_parser',
        ],
        LIBDEPS_PRIVATE=[
            '$BUILD_DIR/merizo/client/clientdriver_network',
            '$BUILD_DIR/merizo/util/concurrency/thread_pool',
            '$BUILD_DIR/merizo/util/net/http_client',
        ],
    )
else:
    fmEnv.Library(
        target='free_mon_merizod',
        source=[
            'free_mon_commands_stub.cpp',
            'free_mon_stub.cpp',
        ],
        LIBDEPS_PRIVATE=[
            'free_mon',
            '$BUILD_DIR/merizo/base',
            '$BUILD_DIR/merizo/db/commands',
            '$BUILD_DIR/merizo/db/auth/auth',
            '$BUILD_DIR/merizo/db/auth/authprivilege',
        ],
    )


fmEnv.CppUnitTest(
    target='free_mon_test',
    source=[
        'free_mon_controller_test.cpp',
        'free_mon_queue_test.cpp',
        'free_mon_storage_test.cpp',
    ],
    LIBDEPS=[
        '$BUILD_DIR/merizo/db/auth/authmocks',
        '$BUILD_DIR/merizo/db/repl/replmocks',
        '$BUILD_DIR/merizo/db/repl/storage_interface_impl',
        '$BUILD_DIR/merizo/db/service_context_d_test_fixture',
        '$BUILD_DIR/merizo/db/service_context_d',
        '$BUILD_DIR/merizo/executor/thread_pool_task_executor_test_fixture',
        '$BUILD_DIR/merizo/util/clock_source_mock',
        'free_mon',
    ],
)
