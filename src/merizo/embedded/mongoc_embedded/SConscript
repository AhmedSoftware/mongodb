# -*- mode: python; -*-

import libdeps

Import("env")
Import("get_option")

env = env.Clone()

if not env['MONGO_HAVE_LIBMONGOC']:
    Return()

if get_option('install-mode') == 'hygienic':
    env.AutoInstall(
        'share/doc/merizoc_embedded',
        source=[
            '#/LICENSE-Community.txt',
            '../LICENSE-Embedded.txt',
        ],
        INSTALL_ALIAS=[
            'embedded-dev',
        ],
    )

def create_merizoc_env(env):
    merizocEnv = env.Clone()
    if merizocEnv['MONGO_HAVE_LIBMONGOC'] == "framework":
        merizocEnv.AppendUnique(FRAMEWORKS=['bson', 'merizoc'])
    else:
        merizocEnv.AppendUnique(LIBS=['bson-1.0', 'merizoc-1.0'])
    return merizocEnv

merizocEmbeddedEnv = create_merizoc_env(env)

merizocEmbeddedEnv.AppendUnique(
    CPPDEFINES=[
        'MONGOC_EMBEDDED_COMPILING',
     ],
)

if get_option('link-model') == 'static':
    merizocEmbeddedEnv.AppendUnique(
        CPPDEFINES=[
            'MONGOC_EMBEDDED_STATIC',
        ],
    )

# Please see the note in ../merizo_embedded/SConscript about how to
# interpret and adjust the current and compatibility versinos.
merizocEmbeddedEnv.AppendUnique(
    SHLINKFLAGS=[
        '$MONGO_EXPORT_FILE_SHLINKFLAGS',
    ],
)

if merizocEmbeddedEnv.TargetOSIs('darwin'):
    # Please see the note in ../merizo_embedded/SConscript about how to
    # interpret and adjust the current and compatibility versinos.
    merizocEmbeddedEnv.AppendUnique(
        SHLINKFLAGS=[
            '-Wl,-current_version,1',
            '-Wl,-compatibility_version,1',
        ],
    )

merizocEmbeddedTargets = merizocEmbeddedEnv.Library(
    target='merizoc_embedded',
    source=[
        'merizoc_embedded.cpp',
    ],
    LIBDEPS=[
        # No LIBDEPS or LIBDEPS_PRIVATE to merizo libraries are allowed in this library. They would get duplicated in merizo_embedded_capi.
        '$BUILD_DIR/merizo/embedded/merizo_embedded/merizo_embedded',
    ],
    INSTALL_ALIAS=[
        'embedded-dev',
    ],
)

if get_option('install-mode') == 'hygienic':
    env.AutoInstall(
        'include/merizoc_embedded/v1/merizoc_embedded',
        source=['merizoc_embedded.h'],
        INSTALL_ALIAS=[
            'embedded-dev',
        ],
    )

yamlEnv = env.Clone()
yamlEnv.InjectThirdParty(libraries=['yaml'])

if get_option('link-model') != 'dynamic-sdk':
    merizocEmbeddedTestEnv = create_merizoc_env(yamlEnv)
    clientTest = merizocEmbeddedTestEnv.Program(
        target='merizoc_embedded_test',
        source=[
            'merizoc_embedded_test.cpp',
            env.Idlc('merizoc_embedded_test.idl')[0],
        ],
        LIBDEPS_PRIVATE=[
            '$BUILD_DIR/merizo/base',
            '$BUILD_DIR/merizo/db/server_options_core',
            '$BUILD_DIR/merizo/unittest/unittest',
            '$BUILD_DIR/merizo/util/options_parser/options_parser',
            'merizoc_embedded',
        ],
        INSTALL_ALIAS=[
            'embedded-test',
        ],
    )

    env.RegisterUnitTest(clientTest[0]);

# Frameworkization craziness begins here. Honestly, we should do this
# better in the future in some re-usable way, but we need to get this
# thing out the door, so here goes.

# First, we only do this in hygienic mode for the mobile targets,
# which are darwin but not macOS. For all others, we abort here. Maybe
# this should be a build flag? Since we aren't doing this for macOS,
# we can also ignore all the framework version nonsense.
if get_option('link-model') != 'dynamic-sdk' or get_option('install-mode') != 'hygienic' or not env.TargetOSIs('darwin') or env.TargetOSIs('macOS'):
    Return()

frameworkDir = env.Dir('$INSTALL_DIR/Frameworks/merizoc_embedded.framework')
env.Alias('install-embedded-dev', frameworkDir)

resourceDir = frameworkDir
if env.TargetOSIs('macOS'):
    resourceDir = resourceDir.Dir('Resources')

env.Install(
    target=resourceDir,
    source=env.File(
        name=[
            'LICENSE-Community.txt',
            'LICENSE-Embedded.txt',
        ],
        directory=env.Dir('$INSTALL_DIR/share/doc/merizoc_embedded'),
    ),
)

env.Install(
    target=frameworkDir.Dir('Headers'),
    source=env.File('merizoc_embedded.h'),
)

env.InstallAs(
    target=frameworkDir.File('Modules/module.modulemap'),
    source="merizoc_embedded.modulemap"
)

merizocEmbeddedPlist = env.Substfile(
    target="Info.plist",
    source='../Info.plist.in',
    SUBST_DICT=[
        ('@CFBundleExecutable@', 'merizoc_embedded'),
        ('@CFBundleIdentifier@', 'org.merizodb.merizoc-embedded'),
        ('@CFBundleVersion@', env['PLIST_MONGO_BUNDLE_VERSION']),
        ('@CFBundleShortVersionString@', env['PLIST_MONGO_BUNDLE_VERSION']),
        ('@MinimumOSVersion@', env['PLIST_MINIMUM_OS_VERSION'])
    ]
)

env.Install(
    target=resourceDir,
    source=merizocEmbeddedPlist,
)

merizocEmbeddedFwLib = env.InstallAs(
    target=frameworkDir.File('merizoc_embedded'),
    source=merizocEmbeddedTargets[0],
)

env.AddPostAction(
    files=merizocEmbeddedFwLib,
    action=[
        "install_name_tool -delete_rpath @loader_path/../lib $TARGET",
        "install_name_tool -id @rpath/merizoc_embedded.framework/merizoc_embedded $TARGET",
        "install_name_tool -change @rpath/libmerizo_embedded.dylib @rpath/merizo_embedded.framework/merizo_embedded $TARGET",
    ],
)

merizocEmbeddedDSYM = getattr(merizocEmbeddedTargets[0].attributes, "separate_debug_file", None)
if merizocEmbeddedDSYM:
    frameworkDSYMDir = '$INSTALL_DIR/Frameworks/merizoc_embedded.framework.dSYM'
    env.Alias('install-embedded-dev', frameworkDSYMDir)

    env.InstallAs(
        target=frameworkDSYMDir,
        source=merizocEmbeddedDSYM,
    )

merizocEmbeddedBCSymbolMap = getattr(merizocEmbeddedTargets[0].attributes, "bcsymbolmap_file", None)
if merizocEmbeddedBCSymbolMap:
    env.Install(
        target=frameworkDir.Dir('BCSymbolMaps'),
        source=merizocEmbeddedBCSymbolMap,
    )
