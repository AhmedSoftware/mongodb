# -*- mode: python; -*-

import libdeps

Import("env")
Import("get_option")

env = env.Clone()


if get_option('install-mode') == 'hygienic':
    env.AutoInstall(
        'share/doc/merizo_embedded',
        source=[
            '#/LICENSE-Community.txt',
            '#/distsrc/THIRD-PARTY-NOTICES',
            '../LICENSE-Embedded.txt',
        ],
        INSTALL_ALIAS=[
            'embedded-dev',
        ],
    )

merizoEmbeddedEnv = env.Clone()
merizoEmbeddedEnv.AppendUnique(
    CPPDEFINES=[
        'MONGO_EMBEDDED_COMPILING',
    ],
)

if get_option('link-model') == 'static':
    merizoEmbeddedEnv.AppendUnique(
        CPPDEFINES=[
            'MONGO_EMBEDDED_STATIC',
        ],
    )
elif get_option('link-model') == 'dynamic-sdk':
    merizoEmbeddedEnv['LIBDEPS_SHLIBEMITTER'] = libdeps.make_libdeps_emitter(
        'SharedArchive',
        libdeps.dependency_visibility_honored
    )

merizoEmbeddedEnv.AppendUnique(
    SHLINKFLAGS=[
        '$MONGO_EXPORT_FILE_SHLINKFLAGS',
    ],
)

if merizoEmbeddedEnv.TargetOSIs('darwin'):
    # The current version and compatibility are the *minor* ABI
    # version metadata. If you extend (but do not break) the ABI, you
    # should increment current_version but leave compatibiity_version
    # at 1. If you break ABI, you should bump the entire library from
    # v1 to v2, in which case you would then reset both
    # current_version and compatibility_version to 1. You should never
    # need to set compatibility_version to anything but 1.
    merizoEmbeddedEnv.AppendUnique(
        SHLINKFLAGS=[
            '-Wl,-current_version,1',
            '-Wl,-compatibility_version,1',
        ],
    )


merizoEmbeddedTargets = merizoEmbeddedEnv.Library(
    target='merizo_embedded',
    source=[
        'merizo_embedded.cpp',
    ],
    LIBDEPS_PRIVATE=[
        '$BUILD_DIR/merizo/db/service_context',
        '$BUILD_DIR/merizo/rpc/protocol',
        '$BUILD_DIR/merizo/transport/transport_layer_mock',
        '$BUILD_DIR/merizo/embedded/embedded',
    ],
    INSTALL_ALIAS=[
        'embedded-dev',
    ],
)

if get_option('install-mode') == 'hygienic':
    env.AutoInstall(
        'include/merizo_embedded/v1/merizo_embedded',
        source=['merizo_embedded.h'],
        INSTALL_ALIAS=[
            'embedded-dev',
        ],
    )

yamlEnv = env.Clone()
yamlEnv.InjectThirdParty(libraries=['yaml'])

if get_option('link-model') != 'dynamic-sdk':
    merizoEmbeddedTest = yamlEnv.Program(
        target='merizo_embedded_test',
        source=[
            'merizo_embedded_test.cpp',
            env.Idlc('merizo_embedded_test.idl')[0],
        ],
        LIBDEPS_PRIVATE=[
            '$BUILD_DIR/merizo/base',
            '$BUILD_DIR/merizo/db/commands/test_commands_enabled',
            '$BUILD_DIR/merizo/db/server_options_core',
            '$BUILD_DIR/merizo/rpc/protocol',
            '$BUILD_DIR/merizo/unittest/unittest',
            '$BUILD_DIR/merizo/util/net/network',
            '$BUILD_DIR/merizo/util/options_parser/options_parser',
            'merizo_embedded',
        ],
        INSTALL_ALIAS=[
            'embedded-test',
        ],
    )

    env.RegisterUnitTest(merizoEmbeddedTest[0])

# Frameworkization craziness begins here. Honestly, we should do this
# better in the future in some re-usable way, but we need to get this
# thing out the door, so here goes.

# First, we only do this in hygienic mode for the mobile targets,
# which are darwin but not macOS. For all others, we abort here. Maybe
# this should be a build flag? Since we aren't doing this for macOS,
# we can also ignore all the framework version nonsense.
if get_option('link-model') != 'dynamic-sdk' or get_option('install-mode') != 'hygienic' or not env.TargetOSIs('darwin') or env.TargetOSIs('macOS'):
    Return()

frameworkDir = env.Dir('$INSTALL_DIR/Frameworks/merizo_embedded.framework')
env.Alias('install-embedded-dev', frameworkDir)

resourceDir = frameworkDir
if env.TargetOSIs('macOS'):
    resourceDir = resourceDir.Dir('Resources')

env.Install(
    target=resourceDir,
    source=env.File(
        name=[
            'LICENSE-Community.txt',
            'LICENSE-Embedded.txt',
            'THIRD-PARTY-NOTICES',
        ],
        directory=env.Dir('$INSTALL_DIR/share/doc/merizo_embedded'),
    ),
)

env.Install(
    target=frameworkDir.Dir('Headers'),
    source=env.File('merizo_embedded.h')
)

env.InstallAs(
    target=frameworkDir.File('Modules/module.modulemap'),
    source="merizo_embedded.modulemap"
)

merizoEmbeddedPlist = env.Substfile(
    target="Info.plist",
    source='../Info.plist.in',
    SUBST_DICT=[
        ('@CFBundleExecutable@', 'merizo_embedded'),
        ('@CFBundleIdentifier@', 'org.merizodb.merizo-embedded'),
        ('@CFBundleVersion@', env['PLIST_MONGO_BUNDLE_VERSION']),
        ('@CFBundleShortVersionString@', env['PLIST_MONGO_BUNDLE_VERSION']),
        ('@MinimumOSVersion@', env['PLIST_MINIMUM_OS_VERSION'])
    ]
)

env.Install(
    target=resourceDir,
    source=merizoEmbeddedPlist,
)

merizoEmbeddedFwLib = env.InstallAs(
    target=frameworkDir.File('merizo_embedded'),
    source=merizoEmbeddedTargets[0],
)

env.AddPostAction(
    files=merizoEmbeddedFwLib,
    action=[
        "install_name_tool -delete_rpath @loader_path/../lib $TARGET",
        "install_name_tool -id @rpath/merizo_embedded.framework/merizo_embedded $TARGET",
    ]
)

merizoEmbeddedDSYM = getattr(merizoEmbeddedTargets[0].attributes, "separate_debug_file", None)
if merizoEmbeddedDSYM:
    frameworkDSYMDir = '$INSTALL_DIR/Frameworks/merizo_embedded.framework.dSYM'
    env.Alias('install-embedded-dev', frameworkDSYMDir)

    env.InstallAs(
        target=frameworkDSYMDir,
        source=merizoEmbeddedDSYM,
    )

merizoEmbeddedBCSymbolMap = getattr(merizoEmbeddedTargets[0].attributes, "bcsymbolmap_file", None)
if merizoEmbeddedBCSymbolMap:
    env.Install(
        target=frameworkDir.Dir('BCSymbolMaps'),
        source=merizoEmbeddedBCSymbolMap,
    )
